import java.time.Instant
import java.time.LocalDate

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.github.ben-manes.versions' version '0.36.0'
}

repositories {
    mavenCentral()
}

apply plugin: 'idea'

project.sourceCompatibility = JavaVersion.VERSION_14
project.targetCompatibility = JavaVersion.VERSION_14

group = 'uk.gov.justice.probation'

def today = Instant.now()
def todaysDate = LocalDate.now().format('yyyy-MM-dd')
version = System.getenv('CI') ? "${todaysDate}.${System.getenv('CIRCLE_BUILD_NUM')}" : todaysDate

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    agentDeps.transitive = false
}

configurations.all {
    resolutionStrategy.eachDependency {
        DependencyResolveDetails details ->
            if (details.requested.group == 'org.codehaus.groovy') {
                details.useVersion "3.0.2"
                details.because "needed by rest-assured>=4.3"
            }
    }
}

jar {
    baseName = rootProject.name
    manifest {
        attributes(
                'Implementation-Title': rootProject.name, 'Implementation-Version': version
        )
    }
}

springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version = version
            group = group
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
            ]
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'org.projectlombok:lombok'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    implementation group: 'com.google.guava', name: 'guava', version: '30.1-jre'
    implementation 'org.springframework.boot:spring-boot-starter-artemis'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.11.4'
    implementation 'com.amazonaws:amazon-sqs-java-messaging-lib:1.0.8'
    implementation 'org.springframework.cloud:spring-cloud-aws-messaging:2.2.6.RELEASE'

    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.projectreactor:reactor-spring:1.0.1.RELEASE'

    implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:2.6.2'
    implementation group: 'com.microsoft.azure', name: 'applicationinsights-logging-logback', version: '2.6.2'
    implementation 'net.logstash.logback:logstash-logback-encoder:6.6'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-oauth2-client'

    testImplementation(platform("org.junit:junit-bom:5.7.1"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("junit:junit:4.13.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter")
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher") {
        because 'allows tests to run from IDEs that bundle older version of launcher'
    }

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'

    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.24.0'
    testImplementation 'org.assertj:assertj-core:3.19.0'

    testCompile 'io.rest-assured:rest-assured:4.3.3'
    testImplementation 'io.rest-assured:json-path:4.3.3'
    testImplementation 'io.rest-assured:xml-path:4.3.3'
    testCompile "com.github.tomakehurst:wiremock-jre8:2.26.3"
    testCompile 'org.mockito:mockito-core:3.7.7'
    testCompile 'org.apache.activemq:artemis-junit:2.17.0'
    testAnnotationProcessor 'org.projectlombok:lombok'

    agentDeps 'com.microsoft.azure:applicationinsights-agent:3.0.2'
}

test {
    useJUnitPlatform()
    testLogging {
        events "started", "passed", "skipped", "failed", "standardError"
        exceptionFormat "short"
        showStackTraces = true
        showExceptions = true
        showCauses = true

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

task copyAgent(type: Copy) {
    from configurations.agentDeps
    into "$buildDir/libs"
}

task copyAgentConfig(type: Copy) {
    from "applicationinsights.json"
    into "$buildDir/libs"
}

assemble.dependsOn copyAgent
assemble.dependsOn copyAgentConfig
